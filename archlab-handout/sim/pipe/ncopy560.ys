#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Amano Sei
#
# 看了dalao的做法，真的超妙的说...
# 所以这里要尝试在理解了dalao的做法后，自己重新写一遍
# 这是将557版本扩展到8位展开的版本，但是分数只有56.0
# 接下来尝试进一步扩充
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	xorq %rax,%rax		# count = 0;
    iaddq $-8, %rdx
    jge Loop
    iaddq $4, %rdx
    jl LL
    je L4
    iaddq $-2, %rdx
    jl L5
    je L6
    jmp L7
LL:
    iaddq $3, %rdx
    jl Done
    je L1
    iaddq $-2, %rdx
    jl L2
    je L3
Loop:
    mrmovq (%rdi), %r8
    mrmovq 8(%rdi), %r9
    mrmovq 16(%rdi), %r10
    mrmovq 24(%rdi), %r11
    rmmovq %r8, (%rsi)
    rmmovq %r9, 8(%rsi)
    rmmovq %r10, 16(%rsi)
    rmmovq %r11, 24(%rsi)
    andq %r8, %r8
    jle Nposl1
    iaddq $1, %rax
Nposl1:
    andq %r9, %r9
    jle Nposl2
    iaddq $1, %rax
Nposl2:
    andq %r10, %r10
    jle Nposl3
    iaddq $1, %rax
Nposl3:
    andq %r11, %r11
    jle Nposl4
    iaddq $1, %rax
Nposl4:
    mrmovq 32(%rdi), %r8
    mrmovq 40(%rdi), %r9
    mrmovq 48(%rdi), %r10
    mrmovq 56(%rdi), %r11
    rmmovq %r8, 32(%rsi)
    rmmovq %r9, 40(%rsi)
    rmmovq %r10, 48(%rsi)
    rmmovq %r11, 56(%rsi)
    andq %r8, %r8
    jle Nposl5
    iaddq $1, %rax
Nposl5:
    andq %r9, %r9
    jle Nposl6
    iaddq $1, %rax
Nposl6:
    andq %r10, %r10
    jle Nposl7
    iaddq $1, %rax
Nposl7:
    andq %r11, %r11
    jle Nposl8
    iaddq $1, %rax
Nposl8:
    iaddq $0x40, %rsi
    iaddq $0x40, %rdi
    iaddq $-8, %rdx
    jge Loop
    iaddq $4, %rdx
    jl LL
    je L4
    iaddq $-2, %rdx
    jl L5
    je L6
L7:
    mrmovq 48(%rdi), %r14
    rmmovq %r14, 48(%rsi)
    andq %r14, %r14
L6:
    mrmovq 40(%rdi), %r13
    jle Nposf7
    iaddq $1, %rax
Nposf7:
    rmmovq %r13, 40(%rsi)
    andq %r13, %r13
L5:
    mrmovq 32(%rdi), %r12
    jle Nposf6
    iaddq $1, %rax
Nposf6:
    andq %r12, %r12
    rmmovq %r12, 32(%rsi)
L4:
    mrmovq 24(%rdi), %r11
    jle Nposf5
    iaddq $1, %rax
Nposf5:
    andq %r11, %r11
    rmmovq %r11, 24(%rsi)
L3:
    mrmovq 16(%rdi), %r10
    jle Nposf4
    iaddq $1, %rax
Nposf4:
    andq %r10, %r10
    rmmovq %r10, 16(%rsi)
L2:
    mrmovq 8(%rdi), %r9
    jle Nposf3
    iaddq $1, %rax
Nposf3:
    andq %r9, %r9
    rmmovq %r9, 8(%rsi)
L1:
    mrmovq (%rdi), %r8
    jle Nposf2
    iaddq $1, %rax
Nposf2:
    andq %r8, %r8
    rmmovq %r8, (%rsi)
    jle Done
    iaddq $1, %rax
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
